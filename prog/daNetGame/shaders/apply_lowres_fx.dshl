include "shader_global.dshl"
include "dafx_shaders.dshl"
include "upscale_use.dshl"


texture lowres_fx_source_tex;
texture lowres_fx_reactive_mask_tex;
float lowres_fx_overbright = 1;

shader apply_lowres_fx, fast_apply_lowres_fx
{
  ENABLE_ASSERT(ps)
  supports global_frame;

  cull_mode  = none;
  z_test=false;
  z_write = false;
  blend_src = one; blend_dst = sa;

  INIT_ZNZFAR()
  INIT_UPSCALE_SAMPLING()
  USE_UPSCALE_SAMPLING()
  USE_SCREENPOS_TO_LOWRES_TC()

  (ps)
  {
    lowres_fx_source_tex@smp2d = lowres_fx_source_tex;
    lowres_fx_reactive_mask_tex@tex2d = lowres_fx_reactive_mask_tex;
    lowres_fx_overbright@f1 = (lowres_fx_overbright);
  }

  POSTFX_VS(1)

  hlsl(ps) {
    #define lowres_fx_reactive_mask_tex_samplerstate lowres_fx_source_tex_samplerstate

    struct PsOutput
    {
      float4 color : SV_Target0;
      ##if lowres_fx_reactive_mask_tex != NULL
      float4 reactive : SV_Target1;
      ##endif
    };

    PsOutput apply_fx_ps(VsOutput input HW_USE_SCREEN_POS)
    {
      PsOutput result;

      float2 screenpos = GET_SCREEN_POS(input.pos).xy;
      float2 tc = screen_pos_to_lowres_tc(screenpos);

##if shader == fast_apply_lowres_fx
      half4 lowResColorCenter = h4tex2Dlod(lowres_fx_source_tex, float4(tc, 0, 0));
      result.color = float4(lowResColorCenter.rgb * lowres_fx_overbright, lowResColorCenter.a);
      ##if lowres_fx_reactive_mask_tex != NULL
      result.reactive = float4(tex2Dlod(lowres_fx_reactive_mask_tex, float4(tc, 0, 0)).rrr, lowResColorCenter.a);
      ##endif
      return result;
##endif

      half4 lowResR = lowres_fx_source_tex.GatherRed(lowres_fx_source_tex_samplerstate, tc);
      half4 lowResG = lowres_fx_source_tex.GatherGreen(lowres_fx_source_tex_samplerstate, tc);
      half4 lowResB = lowres_fx_source_tex.GatherBlue(lowres_fx_source_tex_samplerstate, tc);
      half4 lowResA = lowres_fx_source_tex.GatherAlpha(lowres_fx_source_tex_samplerstate, tc);

      float4 weight = SampleUpscaleWeight(screenpos.xy);

      float4 res = float4(dot(lowResR,weight),dot(lowResG,weight),dot(lowResB,weight),dot(lowResA,weight));

      result.color = float4(res.rgb * lowres_fx_overbright, res.a);
      ##if lowres_fx_reactive_mask_tex != NULL
      half4 lowResReactive = lowres_fx_reactive_mask_tex.GatherRed(lowres_fx_reactive_mask_tex_samplerstate, tc);
      result.reactive = float4(dot(lowResReactive,weight).xxx, res.a);
      ##endif
      return result;
    }
  }

  compile("target_ps", "apply_fx_ps");
}
