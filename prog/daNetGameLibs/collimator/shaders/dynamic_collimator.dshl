include "dynamic_trans_inc.dshl"
include "glass_gbuffer_inc.dshl"
include "collimator/dynamic_collimator_inc.dshl"

float4 collimator_color_multiplier = (2.5, 1.5, 0.5, 0.);

float4 collimator_u = (1, 0, 0, 1);
float4 collimator_v = (0, 1, 0, 1);
float4 gyro_sight_u = (0, 0, 0, 0);
float4 gyro_sight_v = (0, 0, 0, 0);

shader dynamic_collimator
{
  DYNAMIC_COLLIMATOR_SHADER()
}

shader collimator, gyro_sight {
  ENABLE_ASSERT(ps)
  DYNAMIC_BLOCK_TRANS()
  supports global_const_block;

  texture collimator_tex = material.texture[1];

  if (shader == gyro_sight)
  {
    texture gyro_sight_tex = material.texture[2];
  }

  if (glass_normal_pass == yes)
  {
    dont_render;
  }

  render_trans;
  z_write = false;
  USE_ATEST_1()

  blend_src = sa; blend_dst = one;
  blend_asrc = zero; blend_adst = one;

//---------------------------------------------------
// PS stuff.
//---------------------------------------------------

  USE_HDR(ps)
  INIT_NO_SKINNING()
  if (dyn_model_render_pass != render_pass_normal)
  {
    dont_render;
  }
  SUPPORT_MULTIDRAW(false)
  if (two_sided)
  {
    cull_mode = none;
  }


  channel float3 pos=pos;
  channel color8 norm=norm unsigned_pack;
  channel short2 tc[0]=tc[0] mul_4k;


  USE_DIFFUSE_TC()

  hlsl {
    struct VsOutput {
      VS_OUT_POSITION(pos)
      float3 normal                           : TEXCOORD0;
      float2 diffuseTexcoord                  : TEXCOORD1;
      float3 pointToEye                       : TEXCOORD2;
      float4 collimatorTexcoord               : TEXCOORD3;
##if shader == gyro_sight
      float4 gyroSightTexcoord                : TEXCOORD4;
##endif
      #if SET_UP_MULTIDRAW
      VSOUT_DRAW_ID
      #endif
    };
  }


//---------------------------------------------------
// VS stuff.
//---------------------------------------------------

  hlsl(vs) {
    struct VsInput
    {
      float3 pos                  : POSITION;   // W defaults to 1.
      float4 normal               : NORMAL;
      int2  diffuseTexcoord      : TEXCOORD0;
    };
  }

  if (shader == collimator)
  {
    (vs) {
      collimator_u@f4 = collimator_u;
      collimator_v@f4 = collimator_v;
    }
  }
  else
  {
    (vs) {
      collimator_u@f4 = collimator_u;
      collimator_v@f4 = collimator_v;
      gyro_sight_u@f4 = gyro_sight_u;
      gyro_sight_v@f4 = gyro_sight_v;
    }
  }

  DECL_POSTFX_TC_VS_RT()
  USE_FLOAT_POS_PACKING()
  NO_SKINNING_VS()

  hlsl(vs) {
    VsOutput dynamic_simple_vs(VsInput input HW_USE_INSTANCE_ID)
    {
      VsOutput output;
      #if SET_UP_MULTIDRAW
        INIT_MULTIDRAW(output);
        SET_DRAW_CALL_ID(get_draw_id() & MATERIAL_OFFSET_MASK);
      #endif

      // unpack positions using dynmodel bbox
      input.pos.xyz = unpack_pos(input.pos);

      // unpack texcoord0
      float2 diffuseTexcoord = input.diffuseTexcoord / 4096.;

      // Skinning.

      float3 eyeToPoint;
      float3 stub;

      float3 localDu = float3(1,0,0), localDv = float3(1,0,0);
      instance_skinning(
        input,
        input.pos,
        stub,
        localDu,
        localDv,
        eyeToPoint,
        output.pos,
        stub,
        stub,
        stub);
      //to be removed

      float4 unpackedOutputPos = output.pos;

      output.diffuseTexcoord.xy = diffuseTexcoord;
      output.collimatorTexcoord = float4(
        dot(unpackedOutputPos, collimator_u),
        dot(unpackedOutputPos, collimator_v),
        unpackedOutputPos.zw);
##if shader == gyro_sight
      output.gyroSightTexcoord = float4(
        dot(unpackedOutputPos, gyro_sight_u),
        dot(unpackedOutputPos, gyro_sight_v),
        unpackedOutputPos.zw);
##endif
      return output;
    }
  }
  compile("target_vs", "dynamic_simple_vs");

  if (shader == gyro_sight)
  {
    (ps) { gyro_sight_tex@static = gyro_sight_tex; }
  }

  (ps) {
    collimator_tex@static = collimator_tex;
    // brightness function with clamping between 0.05 and 1 like in WT from dag_color.h
    // sRGB to intensity with NTSC Weights: r=.299 g=.587 b=.114
    collimator_lighting_intensity@f1 = max(0.05, min(1.0, (sun_color_0.x * 0.299 + sun_color_0.y * 0.587 + sun_color_0.z * 0.114) + 2.0 * (sky_color.x * 0.299 + sky_color.y * 0.587 + sky_color.z * 0.114)));
    collimator_color_multiplier@f4 = (collimator_color_multiplier);
  }

  hlsl(ps) {

    float4 dynamic_simple_ps(VsOutput input HW_USE_SCREEN_POS):SV_Target0
    {
      #if SET_UP_MULTIDRAW
        INIT_MULTIDRAW(input);
        SET_DRAW_CALL_ID(GET_DRAW_ID(input) & MATERIAL_OFFSET_MASK);
      #endif

      half alpha = tex2DBindless(get_collimator_tex(), input.diffuseTexcoord.xy).a;
      clip_alpha(alpha);
      half3 color = tex2DprojBindless(get_collimator_tex(), input.collimatorTexcoord).rgb;
##if shader == gyro_sight
      color += tex2DprojBindless(get_gyro_sight_tex(), input.gyroSightTexcoord).rgb;
##endif
      color.rgb = pack_hdr(pow(color, 1 / (1 + collimator_lighting_intensity)) * collimator_color_multiplier.rgb);
      return float4(color, alpha);
    }
  }
  compile("target_ps", "dynamic_simple_ps");
}
